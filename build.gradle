plugins {
    id "application"
    id "eclipse"
    id "org.panteleyev.jpackageplugin" version "1.4.1"
}

wrapper {
    gradleVersion = '7.4.2'
}

group = pGroup
version = pVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url 'http://mvn.topobyte.de'
    }
    mavenCentral()
}

dependencies {
}

mainClassName = 'de.topobyte.HelloSwing'

task copyJars(type: Copy) {
    into "$buildDir/lib-run"
    from jar
}

task copyDeps(type: Copy) {
    into "$buildDir/lib-run"
    from configurations.runtimeClasspath
}

task createRuntime(dependsOn: [copyJars, copyDeps]) {
    description 'Setup the execution environment for the scripts'
}

tasks.jpackage {
    dependsOn("createRuntime")

    input  = "$buildDir/lib-run"
    destination = "$buildDir/dist"

    appName = "test-jpackage"
    vendor = "de.topobyte"

    mainJar = tasks.jar.archiveFileName.get()
    mainClass = mainClassName

    javaOptions = ["-Dfile.encoding=UTF-8"]
    verbose = true

    jpackageEnvironment = [
        "GRADLE_DIR": project.projectDir.absolutePath
    ]

    windows {
        winDirChooser = true
	// this make jpackage override some files such as main.wxs
        resourceDir = "$projectDir/windows"
    }
}
